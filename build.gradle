plugins {
    id 'org.springframework.boot' version '2.6.11'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "org.springdoc.openapi-gradle-plugin" version "1.6.0"
    id "com.github.node-gradle.node" version "3.5.1"
    id 'java'
    id 'idea' // comment out if you don't use IntelliJ IDEA
    //id 'eclipse' // uncomment if you use Eclipse
}

group = 'fi.asteriski'
version = '1.0-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://jcenter.bintray.com/"
    }
}

node {
    download = true
    nodeProjectDir = file("${project.projectDir}/typesPlugin")
}

openApi {
    apiDocsUrl.set("http://localhost:8080/api-docs")
    customBootRun {
        args.set(["--spring.profiles.active=special"])
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.session:spring-session-core'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.13.4'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.4'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.11'
    implementation 'org.springdoc:springdoc-openapi-security:1.6.11'
    // Unfortunately not maintained. See https://github.com/michaelklishin/quartz-mongodb
    implementation "com.novemberain:quartz-mongodb:2.2.0-rc2"
    implementation 'org.keycloak:keycloak-spring-boot-starter:21.0.0'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test:5.7.3'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:3.4.11'
}

tasks.named('test') {
    useJUnitPlatform()
}

bootJar {
    archiveFileName = 'eventsignup.jar'
}

task generateTsTypes(type: NpxTask) {
    dependsOn('generateOpenApiDocs')
    command = "openapi-typescript"
    args = ["${project.projectDir}/build/openapi.json", "--output", "${project.projectDir}/typesPlugin/src/generated/events-types.ts"]
}

task generateTypesVersionNumber(type: NpmTask) {
    args = ["version", new Date().format('yyyyMMdd')+".0.0"]
}

task publishTypesPlugin(type: NpmTask) {
    args = ["publish"]
}
